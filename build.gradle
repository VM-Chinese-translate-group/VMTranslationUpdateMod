plugins {
    id "dev.architectury.legacy.loom" version "dev~0.12.0-SNAPSHOT"
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
}

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}"
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "de.oceanlabs.mcp:mcp_${project.mcp_channel}:${project.mcp_version}@zip"
    forge "net.minecraftforge:forge:${project.forge_version}"
}

processResources {
    inputs.property "version", project.mod_version
    inputs.property "mc_version", project.minecraft_version

    filesMatching("mcmod.info") {
        expand "version": project.mod_version, "mc_version": project.minecraft_version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    //options.release = 8
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publisher {
    apiKeys {
        curseforge(System.getenv("CURSEFORGE_TOKEN"))
        modrinth(System.getenv("MODRINTH_TOKEN"))
    }

    curseID = rootProject.curseforge_id
    modrinthID = rootProject.modrinth_id
    versionType = "release"
    changelog = file("CHANGELOG.md").getText('UTF-8')
    version = "$rootProject.version"
    displayName = "VM Translation Update 1.12.2 $rootProject.version"
    gameVersions = ["1.12.2"]
    curseEnvironment = "client"
    loaders = ["forge"]
    artifact = remapJar
}

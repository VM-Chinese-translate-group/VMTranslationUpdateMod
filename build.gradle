plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id "com.gradleup.shadow" version "8.+" apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
    id "com.hypherionmc.modutils.modfusioner" version "1.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.gradleup.shadow"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
            mappings "dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_patch}"
        }

        implementation("com.google.auto.service:auto-service-annotations:1.1.1")
        annotationProcessor("com.google.auto.service:auto-service:1.1.1")
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}

fusioner {
    packageGroup = rootProject.maven_group
    mergedJarName = "${rootProject.archives_base_name}"
    outputDirectory = "build/merged"
    jarVersion = rootProject.version

    neoforge {
        projectName = "neoforge"
        inputTaskName = "remapJar"
    }

    fabric {
        projectName = "fabric"
        inputTaskName = "remapJar"
    }
}

publisher {
    apiKeys {
        curseforge = System.getenv("CURSEFORGE_TOKEN")
        modrinth = System.getenv("MODRINTH_TOKEN")
    }

    curseID = rootProject.curseforge_id
    modrinthID = rootProject.modrinth_id
    versionType = "release"
    changelog = file("CHANGELOG.md").getText('UTF-8')
    version = "$rootProject.version"
    displayName = "VM Translation Update 1.21.3/4 $rootProject.version"
    gameVersions = ["1.21.3", "1.21.4"]
    curseEnvironment = "client"
    loaders = ["fabric", "neoforge"]
    artifact = fusejars

    modrinthDepends {
        required "cloth-config"
    }

    curseDepends {
        required "cloth-config"
    }
}

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id "com.gradleup.shadow" version "8.+" apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.+"
    id "com.hypherionmc.modutils.modfusioner" version "1.+"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.gradleup.shadow"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"

        implementation("com.google.auto.service:auto-service-annotations:1.1.1")
        annotationProcessor("com.google.auto.service:auto-service:1.1.1")
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}"
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

fusioner {
    packageGroup = rootProject.maven_group
    mergedJarName = "${rootProject.archives_base_name}"
    outputDirectory = "build/merged"
    jarVersion = rootProject.version

    forge {
        projectName = "forge"
        inputTaskName = "remapJar"
    }

    fabric {
        projectName = "fabric"
        inputTaskName = "remapJar"
    }
}

publisher {
    apiKeys {
        curseforge(System.getenv("CURSEFORGE_TOKEN"))
        modrinth(System.getenv("MODRINTH_TOKEN"))
    }

    curseID = rootProject.curseforge_id
    modrinthID = rootProject.modrinth_id
    versionType = "release"
    changelog = file("CHANGELOG.md").getText('UTF-8')
    version = "$rootProject.version"
    displayName = "VM Translation Update 1.18.x $rootProject.version"
    gameVersions = ["1.18", "1.18.1", "1.18.2"]
    curseEnvironment = "client"
    loaders = ["fabric", "forge"]
    artifact = fusejars

    modrinthDepends {
        required "cloth-config"
    }

    curseDepends {
        required "cloth-config"
    }
}
